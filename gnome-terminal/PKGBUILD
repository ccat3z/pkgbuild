# Maintainer: Fabian Bornschein <fabiscafe-at-mailbox-dot-org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-terminal
pkgver=3.50.1
pkgrel=1
pkgdesc="The GNOME Terminal Emulator"
url="https://wiki.gnome.org/Apps/Terminal"
arch=(x86_64)
license=(GPL)
depends=(
  gsettings-desktop-schemas
  libhandy
  vte3
)
makedepends=(
  docbook-xsl
  git
  gnome-shell
  libnautilus-extension
  meson
  yelp-tools
)
groups=(gnome-extra modified)
_commit=5ac3b8e4bd6fa02651b3c23cedb0a7e1cd769655  # tags/3.50.1^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-terminal.git#commit=$_commit" resize.patch)
b2sums=(
  'SKIP'
  51ba721f0f0215565c1eeeb31669e4e459595eaae9e9dd69add3d3c8f2f6b48a9f3922bcc7634d89b9608578f7ce7cbd444789bc07bf6511fcdcd461176aeac8
)

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
  patch -p1 < "$srcdir/resize.patch"
}

build() {
  local meson_options=(
    -D b_lto=false
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
