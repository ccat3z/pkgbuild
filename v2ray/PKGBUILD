# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: pandada8 <pandada8@gmail.com>

github_repo="ccat3z/v2ray-core"
pkgname=v2ray
pkgver=4.34.0.r21.g3e091b081
pkgrel=1
pkgdesc="A platform for building proxies to bypass network restrictions"
groups=('modified')
arch=('x86_64')
url="https://github.com/${github_repo}"
license=('MIT')
depends=('glibc' 'v2ray-domain-list-community' 'v2ray-geoip')
makedepends=('go' 'git')
provides=(v2ray)
conflicts=(v2ray)
backup=(etc/v2ray/config.json)
source=("$pkgname::git+ssh://git@github.com/${github_repo}.git")
sha512sums=('SKIP')

pkgver() {
  cd $srcdir/$pkgname

  # cutting off 'v' prefix that presents in the git tag
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $srcdir/$pkgname
  sed -i 's|/usr/local/bin|/usr/bin|;s|/usr/local/etc|/etc|' release/config/systemd/system/*.service
}

build() {
  cd $srcdir/$pkgname
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  go build -o v2ray ./main
  go build -o v2ctl ./infra/control/main
}

package() {
  cd $srcdir/$pkgname
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/v2ray/LICENSE
  install -Dm644 release/config/systemd/system/v2ray.service "$pkgdir"/usr/lib/systemd/system/v2ray.service
  install -Dm644 release/config/systemd/system/v2ray@.service "$pkgdir"/usr/lib/systemd/system/v2ray@.service
  install -Dm644 release/config/*.json -t "$pkgdir"/etc/v2ray/
  install -Dm755 v2ray -t "$pkgdir"/usr/bin/
  install -Dm755 v2ctl -t "$pkgdir"/usr/bin/
}
