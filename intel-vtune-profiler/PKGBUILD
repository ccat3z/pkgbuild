# Maintainer: ccat3z <fzhang.chn@foxmail.com>

pkgname=intel-vtune-profiler
pkgver=2022.1.0.98
pkgrel=1
pkgdesc="Intel® VTune™ Profiler"
arch=('x86_64')
url="https://www.intel.com/content/www/us/en/developer/tools/oneapi/vtune-profiler.html"
license=('custom')
depends=(
    'libnotify' 'libxcb' 'xdg-utils' 'libdrm' 'gtk3' 'mesa' 'at-spi2-core' 'nss'
    'glib2'
)
conflicts=('intel-vtune-profiler-standalone')
options=("!strip")
source=(
    "file://l_oneapi_vtune_p_${pkgver}_offline.sh"
)
sha256sums=(
    '3b1d0c62150d8585de81344c78293a91e5df1b8f57b1d80f5e84a4abfc8d4382'
)

prepare() {
    cd "$srcdir"

    [ ! -d installer_home ] || rm -rf installer_home
    mkdir installer_home
    cd installer_home

    HOME=$PWD $srcdir/l_oneapi_vtune_p_${pkgver}_offline.sh -a \
        --silent \
        --eula accept \
        --install-dir "$PWD/vtune" \
        --ignore-errors
}

package() {
    cd "$pkgdir"
    mkdir opt
    cp -R "$srcdir/installer_home/vtune" "opt/vtune"
}

